//file:noinspection SpellCheckingInspection
import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'java-library'
    id 'jacoco'
    id 'jacoco-report-aggregation'
    id 'org.springframework.boot' version '2.7.8' apply false
    id 'net.researchgate.release' version '3.0.2'
    id "io.spring.dependency-management" version "1.1.0"
}

ext {
    moduleProjects = subprojects.findAll { it.name.startsWith("hutool-plus-") }
}

configure(allprojects) { project ->
    apply plugin: "io.spring.dependency-management"

    ext {
        asposeWordsVersion = '23.1'
        assertjVersion = '3.24.2'
        hutoolVersion = '5.8.15'
        imgscalrVersion = '4.2'
        indriyaVersion = '2.1.4'
        iTextPdfVersion = '7.1.10'
        iTextPdfofficeVersion = '2.0.4'
        jsr305Version = '3.0.2'
        log4j2Version = '2.19.0'
        logCaptorVersion = '2.7.10'
        ofdrwVersion = '1.20.2'
        pdfboxVersion = '2.0.27'
        poiVersion = '5.2.3'
        springVersion = '5.3.25'
        tikaVersion = '2.7.0'
    }

    dependencyManagement {
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
            mavenBom "cn.hutool:hutool-bom:${hutoolVersion}"
        }

        dependencies {
            dependency("org.springframework:spring-core:${springVersion}") {
                exclude "org.springframework:spring-jcl"
            }

            dependency("org.apache.logging.log4j:log4j-api:${log4j2Version}")
            dependency("org.apache.logging.log4j:log4j-core:${log4j2Version}")

            dependency("org.apache.poi:poi:${poiVersion}")
            dependency("org.apache.poi:poi-ooxml-full:${poiVersion}")
            dependency("org.apache.poi:poi-scratchpad:${poiVersion}")

            dependencySet(group: 'org.apache.tika', version: "${tikaVersion}") {
                entry 'tika-core'
                entry 'tika-parser-pdf-module'
                entry 'tika-parser-text-module'
                entry 'tika-parser-microsoft-module'
                entry 'tika-parser-miscoffice-module'
            }

            dependency("org.ofdrw:ofdrw-converter:${ofdrwVersion}") {
                exclude "org.apache.logging.log4j:log4j-slf4j-impl"
            }

            dependency("com.google.code.findbugs:jsr305:${jsr305Version}")
            dependency("com.itextpdf:pdfoffice:${iTextPdfofficeVersion}")
            dependency("com.itextpdf:kernel:${iTextPdfVersion}")
            dependency("com.luhuiguo:aspose-words:${asposeWordsVersion}")
            dependency("io.github.hakky54:logcaptor:${logCaptorVersion}")
            dependency("org.apache.pdfbox:pdfbox:${pdfboxVersion}")
            dependency("org.imgscalr:imgscalr-lib:${imgscalrVersion}")
            dependency("tech.units:indriya:${indriyaVersion}")
            dependency("org.assertj:assertj-core:${assertjVersion}")

        }

        generatedPomCustomization {
            enabled = false
        }
        resolutionStrategy {
            cacheChangingModulesFor 0, "seconds"
        }
        repositories {
            maven { url 'https://maven.aliyun.com/repository/public' }
            mavenCentral()
            maven { url 'https://repo.itextsupport.com/releases/' }
        }
    }

    configurations.configureEach {
        resolutionStrategy {
            cacheChangingModulesFor 0, "seconds"
            cacheDynamicVersionsFor 0, "seconds"
        }
    }
}

configure(rootProject) {
    apply from: "${rootDir}/gradle/publications.gradle"

    group "plus.hutool"
    description = "A great Java tool library based on Hutool library"

    tasks.named('check') {
        dependsOn tasks.named('testCodeCoverageReport', JacocoReport)
    }

    dependencies {
        api(project(":hutool-plus-core"))
        api(project(":hutool-plus-extra"))
        api(project(":hutool-plus-media"))
        api(project(":hutool-plus-spring5"))
    }
}

configure(moduleProjects) { project ->
    apply from: "${rootDir}/gradle/hutool-plus-module.gradle"
    group = "plus.hutool"

    dependencies {
        implementation("org.apache.logging.log4j:log4j-api")
        implementation("org.apache.logging.log4j:log4j-core")
        implementation("com.fasterxml.jackson.core:jackson-annotations")
        implementation("org.slf4j:jcl-over-slf4j")
        testImplementation("org.assertj:assertj-core")
        testImplementation("org.junit.jupiter:junit-jupiter")
        testImplementation("org.mockito:mockito-junit-jupiter")
        testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
    }
}

subprojects {
    dependencies {
        compileOnly("org.projectlombok:lombok")
        annotationProcessor("org.projectlombok:lombok")
        implementation("com.google.code.findbugs:jsr305")
    }
}
